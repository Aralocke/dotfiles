# ZSH Environment (Theme)
# A lot of this is based on or uses parts found in the ZSH Theme
# "Bereau" including the get_spaces and right prompt component.

# keep TONS of history
export HISTSIZE=4096

# Export the manpages path on the local system
export MANPATH="/usr/local/man:$MANPATH"

# Path configuration
# All of the locations that should be added to the binary path for the users
HOMEBREW="/usr/local/bin:/usr/local/sbin"

# Export the user binary path before any more execution
export PATH="${HOMEBREW}:/usr/bin:/bin:/usr/sbin:/sbin"

# Enable colors in prompt
autoload -U colors && colors

setopt nocorrectall;
setopt correct

# Configure what we see in the git prompt read out
ZSH_THEME_GIT_PROMPT_PREFIX="[git:"
ZSH_THEME_GIT_PROMPT_SUFFIX="]$reset_color"
ZSH_THEME_GIT_PROMPT_DIRTY="$fg[red]+"
ZSH_THEME_GIT_PROMPT_CLEAN="$fg[green]"

# adds the current branch name in green
function git_prompt_info() {
  ref=$(git symbolic-ref HEAD 2> /dev/null) || return
  echo "$(parse_git_dirty)$ZSH_THEME_GIT_PROMPT_PREFIX$(current_branch)$ZSH_THEME_GIT_PROMPT_SUFFIX"
}

# expand functions in the prompt
setopt prompt_subst

autoload -U promptinit
promptinit

export CLICOLOR=1

# Configure how the username (left-most) part of the prompt looks
_USERNAME="[%{$fg[white]%}%n%{$reset_color%}@%{$fg[white]%}%m%{$reset_color%}"

# Configure what the path looks like. In most cases this should be
# the full working path of wher eyou currently are.
_PATH="%{$fg[yellow]%}%~%{$reset_color%}]"

load_prompt() {
  local PS1_LEFT="$_USERNAME $_PATH"
  local GIT_PROMPT=$(git_prompt_info)
  local PS1_RIGHT="[%{$fg[white]%}%@%{$reset_color%}]"

  local gitwidth
  if [ ${#GIT_PROMPT} != 0 ]; then
      ((gitwidth=${#GIT_PROMPT} - 12))
  else
      gitwidth=0
  fi

  local STR="$PS1_LEFT${GIT_PROMPT}$PS1_RIGHT"
  local zero='%([BSUbfksu]|([FB]|){*})'
  local LENGTH=${#${(S%%)STR//$~zero/}}
  (( LENGTH = ${COLUMNS} - ${LENGTH} + ${gitwidth} - 3))

  local spacing=""
  for i in {1..$LENGTH}; do
    spacing="${spacing} "
  done

  print; print -rP "$PS1_LEFT$spacing$GIT_PROMPT$PS1_RIGHT"
}

# These are the variales that are actually expanded into the prompt.
# By the time these are both written, the primary prompt showing the
# user, path, and clock have already been written.
export PROMPT='%{$fg[green]%}â†’%{$reset_color%} '

# A short explanation of how this method actually works.
# The precmd hook will be called before the shell writes the prompt
# to this screen, this gives us the opportunity to hook in and write
# a line to the screen before the prompt executes.
autoload -U add-zsh-hook
add-zsh-hook precmd load_prompt
